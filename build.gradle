import org.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.19'
    }
}

plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
}

def versionYaml = new Yaml().load(new File("version.yml").newInputStream())
group = 'com.serverless'
version = versionYaml.v

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

ext {
    awsLambdaJavaCoreVersion = '1.2.1'
    awsLambdaJavaLog4jVersion = '1.0.1'
    awssdkBomVersion = '2.15.2'
    lombokVersion = '1.18.12'
}

dependencies {
    api(
            "com.amazonaws:aws-lambda-java-core:$awsLambdaJavaCoreVersion",
            "com.amazonaws:aws-lambda-java-log4j:$awsLambdaJavaLog4jVersion",
    )

    implementation(
            platform("software.amazon.awssdk:bom:$awssdkBomVersion"),
            "software.amazon.awssdk:dynamodb",
            "software.amazon.awssdk:dynamodb-enhanced",
            "org.projectlombok:lombok:$lombokVersion",
    )

    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

task buildZip(type: Zip) {
    archiveBaseName = "products-api"
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

build.dependsOn buildZip
