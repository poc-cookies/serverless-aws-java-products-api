plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    api (
        'com.amazonaws:aws-lambda-java-core:1.2.1',
        'com.amazonaws:aws-lambda-java-log4j:1.0.1',
    )

    implementation(
            platform('software.amazon.awssdk:bom:2.15.2'),
            'software.amazon.awssdk:dynamodb',
            'software.amazon.awssdk:dynamodb-enhanced',
            'org.projectlombok:lombok:1.18.12',
    )

    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

// Task for building the zip file for upload
task buildZip(type: Zip) {
    // Using the Zip API from gradle to build a zip file of all the dependencies
    //
    // The path to this zip file can be set in the serverless.yml file for the
    // package/artifact setting for deployment to the S3 bucket
    //
    // Link: https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Zip.html

    // set the base name of the zip file
    archiveBaseName = "products-api"
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

build.dependsOn buildZip
